<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q130439: Code Comparison: Visual FoxPro Grid &amp; Clipper/DOS Tbrowse | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q130439: Code Comparison: Visual FoxPro Grid &amp; Clipper/DOS Tbrowse" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/130/Q130439/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/130/Q130439/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q130439: Code Comparison: Visual FoxPro Grid &amp; Clipper/DOS Tbrowse","url":"http://jeffpar.github.io/kbarchive/kb/130/Q130439/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q130439-code-comparison-visual-foxpro-grid--clipperdos-tbrowse">Q130439: Code Comparison: Visual FoxPro Grid &amp; Clipper/DOS Tbrowse</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q130439
Product(s): Microsoft FoxPro
Version(s): WINDOWS:3.0
Operating System(s): 
Keyword(s): kb3rdparty kbcode
Last Modified: 03-AUG-2000

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, version 3.0 
-------------------------------------------------------------------------------

SUMMARY
=======

CA-Clipper version 5.x has five predefined classes built into its language. One
of these is the Tbrowse class, which is similar to Visual FoxPro's Grid class.
This article compares the two.

NOTE: This article only describes how to create a Grid control in a program.
Grids can also be created visually through drag-drop and the Visual Class
Designer.

MORE INFORMATION
================

Clipper actually provides two Tbrowse classes - TbrowseDB() and TbrowseNew().
TbrowseDB() browses tables, and TbrowseNew() browses other data sources, such as
text files and arrays.

Below are comparisons of how to browse a table that contains three columns. In
each comparison, the Clipper code is shown followed by the equivalent program in
Visual FoxPro.

NOTE: Run the following program first to create the TEST table used in the
comparisons.

     *** Code to create the TEST table ***
     &amp;&amp;   In Clipper, execute these 5 lines:
     &amp;&amp;   aDbf := {}
     &amp;&amp;   AADD(aDbf, { "Fld1", "N", 3, 0 })
     &amp;&amp;   AADD(aDbf, { "Fld2", "C", 4, 0 })
     &amp;&amp;   AADD(aDbf, { "Fld3", "C", 20, 0 })
     &amp;&amp;   DBCREATE("Test",aDbf)

     &amp;&amp;   In FoxPro, execute the next line:
     &amp;&amp;   CREATE TABLE Test (fld1 n(3),fld2 c(4),fld3 c(20))

     USE Test
     FOR i=1 TO 50
       APPEND BLANK
       REPLACE Fld1 WITH i
       REPLACE Fld2 WITH ALLTRIM(STR(i))
       REPLACE Fld3 WITH REPLICATE(CHR(64+i),20)
     NEXT
     CLOSE ALL
     *** End of code to create TEST table ***

Clipper
-------

     #include "inkey.ch"
     #include "setcurs.ch"

     #define  MYCOLORS   "BG/B,RB+/W,W/N,N,GR+/W,G+/B,R+/B,GR+/B"
     #define  HEADSEP    CHR(205) + CHR(209) + CHR(205)
     #define  COLSEP     CHR(32)  + CHR(179) + CHR(32)

     FUNCTION Main()
        LOCAL b, column, nKey
        SET SCOREBOARD OFF
        SET DATE       BRITISH
        SET CONFIRM    ON

        USE test

        SETCURSOR(SC_NONE)
        SETCOLOR("N/W")
        SCROLL()
        SETCOLOR("BG/B,RB+/W,,,BG/N")
        SCROLL( 2, 6, 22, 72 )
        @  2, 6 TO 22,72 DOUBLE

        b := TBrowseDB( 3, 7, 21, 71 )
        b:colorSpec := MYCOLORS
        b:colSep  := COLSEP
        b:headSep := HEADSEP

        // Custom Skipper
        b:skipBlock     := {|nRecs| Skipper( nRecs )}

        // TBColumn objects
        column := TBColumnNew( "Field 1", {|| test-&gt;fld1} )
        b:addColumn( column )
        column := TBColumnNew( "Field 2", {|| test-&gt;fld2} )
        b:addColumn( column )
        column := TBColumnNew( "Field 3", {|| test-&gt;fld3} )
        b:addColumn( column )

        WHILE .T.
           WHILE ( !b:stabilize() )
              nKey := INKEY()
              IF ( nKey != 0 )
                 EXIT
              ENDIF
           END

           IF ( b:stable )
              nKey := INKEY(0)
           ENDIF

           // Process key
           IF ( nKey == K_DOWN )
              b:down()
           ELSEIF ( nKey == K_UP )
              b:up()
           ELSEIF ( nKey == K_PGDN )
              b:pageDown()
           ELSEIF ( nKey == K_PGUP )
              b:pageUp()
           ELSEIF ( nKey == K_CTRL_PGUP )
              b:goTop()
           ELSEIF ( nKey == K_CTRL_PGDN )
           b:goBottom()
           ELSEIF ( nKey == K_RIGHT )
              b:right()
           ELSEIF ( nKey == K_LEFT )
              b:left()
           ELSEIF ( nKey == K_HOME )
              b:home()
           ELSEIF ( nKey == K_END )
              b:end()
           ELSEIF ( nKey == K_CTRL_LEFT )
              b:panLeft()
           ELSEIF ( nKey == K_CTRL_RIGHT )
              b:panRight()
           ELSEIF ( nKey == K_CTRL_HOME )
              b:panHome()
           ELSEIF ( nKey == K_CTRL_END )
           b:panEnd()
           ELSEIF ( nKey == K_ESC )
              SCROLL()
              EXIT
           ENDIF
        END
     RETURN NIL

     STATIC FUNCTION Skipper( nRequest )
        LOCAL nActually := 0

        IF (nRequest == 0)
           SKIP 0
        ELSEIF (nRequest &gt; 0) .AND. (!EOF())
           WHILE (nActually &lt; nRequest)
              SKIP
              IF EOF()
                 SKIP -1
                 EXIT
              ENDIF
              nActually++
           END
        ELSEIF (nRequest &lt; 0)
           WHILE (nActually &gt; nRequest)
              SKIP -1
              IF BOF()
              EXIT
              ENDIF
              nActually--
           END
        ENDIF
        RETURN nActually

Visual FoxPro
-------------

     USE test
     oFormwgrid = CREATEOBJECT("MyForm")
     oFormwgrid.show
     READ EVENTS

     DEFINE CLASS MyForm AS Form
       Width=400
       Height=250
       ADD OBJECT b AS MyGrid
     ENDDEFINE

     DEFINE CLASS MyGrid AS Grid
       Left=7
       Top=1
       Width=340
       Height=240
       ColumnCount=3

       Column1.ControlSource='fld1'
       Column2.ControlSource='fld2'
       Column3.ControlSource='fld3'
       Column1.Width=50
       Column3.Width=200

       PROCEDURE init
         This.Column1.Header1.Caption='Field 1'
         This.Column2.Header1.Caption='Field 2'
         This.Column3.Header1.Caption='Field 3'
       ENDPROC
     ENDDEFINE

Clipper is an MS-DOS-based product, so you don't need to create a window for the
browse. However, because Visual FoxPro is a Windows-based product, you need to
create a window for it.

The DEFINE CLASS section creates a subclass of the Form class, and the
oFormwgrid=CREATEOBJECT("MyForm") line creates an instance of that object.

Visual FoxPro
-------------

     oFormwgrid=CREATEOBJECT("MyForm")
     oFormwgrid.show

     ..

     DEFINE CLASS MyForm AS Form

     ..

     ENDDEFINE

Once you have created a window, you can add the Grid to it. Again, you will
sub-class the Grid class and create an instance of it with the ADD OBJECT b AS
MyGrid line.

Clipper
-------

     b := TBrowseDB( 3, 7, 21, 71 )

Visual FoxPro
-------------

     ...

     ADD OBJECT b AS MyGrid

     ...

     DEFINE CLASS MyGrid AS Grid
       Left=7
       Top=1
       Width=340
       Height=240
       ColumnCount=3

     ...

     ENDDEFINE

Now you can add columns to the Grid. Clipper creates a new column in the Tbrowse
by creating an instance of the TBColumnNew class and then passing its handle to
the addcolumn method of the Tbrowse.

In Visual FoxPro, you already set the property 'ColumnCount' to 3 in the
sub-class, so you can just assign Column X's ControlSource property to the
desired value. While the Grid is being initialized, you can set the titles of
the column headers in the PROCEDURE init.

Clipper
-------

     column := TBColumnNew( "Field 1", {|| test-&gt;fld1 })
     b:addColumn( column )
     column := TBColumnNew( "Field 2", {|| test-&gt;fld2 })
     b:addColumn( column )
     column := TBColumnNew( "Field 3", {|| test-&gt;fld3 })
     b:addColumn( column )

Visual FoxPro
-------------

     Column1.ControlSource='Test.fld1'
     Column2.ControlSource='Test.fld2'
     Column3.ControlSource='Test.fld3'

     PROCEDURE init
       This.Column1.Header1.Caption='Field 1'
       This.Column2.Header1.Caption='Field 2'
       This.Column3.Header1.Caption='Field 3'
     ENDPROC

The objects properties and methods are set in both products so you can now give
it life. Clipper does this through the stabilize method, DO .. WHILE loops and
INKEY(). Visual FoxPro ties in with the Windows event model so you can just
issue the READ EVENTS command.

Clipper
-------

     WHILE ( !b:stabilize() )
       nKey := INKEY()
       IF ( nKey != 0 )
         EXIT
       ENDIF
     END

     IF ( b:stable )
       nKey := INKEY(0)
     ENDIF

Visual FoxPro
-------------

     READ EVENTS

Clipper/DOS is manufactured by Computer Associates, a vendor independent of
Microsoft; we make no warranty, implied or otherwise, regarding this product's
performance or reliability.

Additional query words: VFoxWin ca clipper for dos 5.01 akz

======================================================================
Keywords          : kb3rdparty kbcode 
Technology        : kbVFPsearch kbAudDeveloper kbVFP300
Version           : WINDOWS:3.0

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>