<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q189633: HOWTO: Query a Service for Status and Configuration | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q189633: HOWTO: Query a Service for Status and Configuration" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/189/Q189633/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/189/Q189633/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q189633: HOWTO: Query a Service for Status and Configuration","url":"http://jeffpar.github.io/kbarchive/kb/189/Q189633/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q189633-howto-query-a-service-for-status-and-configuration">Q189633: HOWTO: Query a Service for Status and Configuration</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q189633
Product(s): Microsoft Visual Basic for Windows
Version(s): WINDOWS:5.0,6.0
Operating System(s): 
Keyword(s): kbOSWinNT kbOSWin2000 kbRegistry kbVBp kbVBp500 kbVBp600 kbGrpDSVB kbDSupport
Last Modified: 11-JAN-2001

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Control Creation Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Professional Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Enterprise Edition for Windows, versions 5.0, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

This article provides sample code that demonstrates how to query a Windows 2000
or Windows NT Service's Status and Configuration from Visual Basic.

MORE INFORMATION
================

Step-by-Step Example
--------------------

1. Start a New Standard EXE Project in Visual Basic. Form1 is created by
  default.

2. Add a Text Box, Command Button, and a List Box to Form1.

3. Remove "Text1" from the Text property of Text1.

4. Paste the following code into the declarations section of Form1:

     Option Explicit

     Private Declare Function CloseServiceHandle Lib "advapi32.dll" _
        (ByVal hSCObject As Long) As Long

     Private Declare Function QueryServiceStatus Lib "advapi32.dll" _
        (ByVal hService As Long, _
        lpServiceStatus As SERVICE_STATUS) As Long

     Private Declare Function OpenService Lib "advapi32.dll" _
        Alias "OpenServiceA" _
        (ByVal hSCManager As Long, _
        ByVal lpServiceName As String, _
        ByVal dwDesiredAccess As Long) As Long

     Private Declare Function OpenSCManager Lib "advapi32.dll" _
        Alias "OpenSCManagerA" _
        (ByVal lpMachineName As String, _
        ByVal lpDatabaseName As String, _
        ByVal dwDesiredAccess As Long) As Long

     Private Declare Function QueryServiceConfig Lib "advapi32.dll" _
        Alias "QueryServiceConfigA" _
        (ByVal hService As Long, _
        lpServiceConfig As Byte, _
        ByVal cbBufSize As Long, _
        pcbBytesNeeded As Long) As Long

     Private Declare Sub CopyMemory Lib "KERNEL32" Alias "RtlMoveMemory" _
        (hpvDest As Any, _
        hpvSource As Any, _
        ByVal cbCopy As Long)

     Private Declare Function lstrcpy Lib "KERNEL32" Alias "lstrcpyA" _
        (ByVal lpString1 As String, _
        ByVal lpString2 As Long) As Long

     Private Type SERVICE_STATUS
        dwServiceType As Long
        dwCurrentState As Long
        dwControlsAccepted As Long
        dwWin32ExitCode As Long
        dwServiceSpecificExitCode As Long
        dwCheckPoint As Long
        dwWaitHint As Long
     End Type

     Private Type QUERY_SERVICE_CONFIG
        dwServiceType As Long
        dwStartType As Long
        dwErrorControl As Long
        lpBinaryPathName As Long 'String
        lpLoadOrderGroup As Long ' String
        dwTagId As Long
        lpDependencies As Long 'String
        lpServiceStartName As Long 'String
        lpDisplayName As Long  'String
     End Type

     Private Const SERVICE_STOPPED = &amp;H1
     Private Const SERVICE_START_PENDING = &amp;H2
     Private Const SERVICE_STOP_PENDING = &amp;H3
     Private Const SERVICE_RUNNING = &amp;H4
     Private Const SERVICE_CONTINUE_PENDING = &amp;H5
     Private Const SERVICE_PAUSE_PENDING = &amp;H6
     Private Const SERVICE_PAUSED = &amp;H7
     Private Const SERVICE_ACCEPT_STOP = &amp;H1
     Private Const SERVICE_ACCEPT_PAUSE_CONTINUE = &amp;H2
     Private Const SERVICE_ACCEPT_SHUTDOWN = &amp;H4
     Private Const SC_MANAGER_CONNECT = &amp;H1
     Private Const SERVICE_INTERROGATE = &amp;H80
     Private Const GENERIC_READ = &amp;H80000000
     Private Const ERROR_INSUFFICIENT_BUFFER = 122

     Private Sub Command1_Click()
        Dim hSCM  As Long
        Dim hSVC As Long
        Dim pSTATUS As SERVICE_STATUS
        Dim udtConfig As QUERY_SERVICE_CONFIG
        Dim lRet As Long
        Dim lBytesNeeded As Long
        Dim sTemp As String
        Dim pFileName As Long

     List1.Clear

        ' Open The Service Control Manager
        '
        hSCM = OpenSCManager(vbNullString, vbNullString, SC_MANAGER_CONNECT)
        If hSCM = 0 Then
           MsgBox "Error - " &amp; Err.LastDllError
        End If

        ' Open the specific Service to obtain a handle
        '
        hSVC = OpenService(hSCM, Trim(Text1.Text), GENERIC_READ)
           If hSVC = 0 Then
              MsgBox "Error - " &amp; Err.LastDllError
              GoTo CloseHandles
           End If

        ' Fill the Service Status Structure
        '
        lRet = QueryServiceStatus(hSVC, pSTATUS)
        If lRet = 0 Then
           MsgBox "Error - " &amp; Err.LastDllError
           GoTo CloseHandles
        End If

        ' Report the Current State
        '
        Select Case pSTATUS.dwCurrentState
        Case SERVICE_STOPPED
           sTemp = "The Service is Stopped"
        Case SERVICE_START_PENDING
           sTemp = "The Service Being Started"
        Case SERVICE_STOP_PENDING
           sTemp = "The Service is in the process of being stopped"
        Case SERVICE_RUNNING
           sTemp = "The Service is Running"
        Case SERVICE_CONTINUE_PENDING
           sTemp = "The Service is in the process of being Continued"
        Case SERVICE_PAUSE_PENDING
           sTemp = "The Service is in the process of being Paused"
        Case SERVICE_PAUSED
           sTemp = "The Service is Paused"
        Case SERVICE_ACCEPT_STOP
           sTemp = "The Service is Stopped"
        Case SERVICE_ACCEPT_PAUSE_CONTINUE
           sTemp = "The Service is "
        Case SERVICE_ACCEPT_SHUTDOWN
           sTemp = "The Service is being Shutdown"
        End Select

        List1.AddItem "Service Status : " &amp; sTemp

        ' Call QueryServiceConfig with 1 byte buffer to generate an error
        ' that returns the size of a buffer we need.
        '
        ReDim abConfig(0) As Byte
        lRet = QueryServiceConfig(hSVC, abConfig(0), 0&amp;, lBytesNeeded)
        If lRet = 0 And Err.LastDllError &lt;&gt; ERROR_INSUFFICIENT_BUFFER Then
           MsgBox "Error - " &amp; Err.LastDllError
        End If

        ' Redim our byte array to the size necessary and call
        ' QueryServiceConfig again
        '
        ReDim abConfig(lBytesNeeded) As Byte
        lRet = QueryServiceConfig(hSVC, abConfig(0), lBytesNeeded, _
           lBytesNeeded)
        If lRet = 0 Then
           MsgBox "Error - " &amp; Err.LastDllError
           GoTo CloseHandles
        End If

        ' Fill our Service Config User Defined Type.
        '
        CopyMemory udtConfig, abConfig(0), Len(udtConfig)

        List1.AddItem "Service Type: " &amp; udtConfig.dwServiceType
        List1.AddItem "Service Start Type: " &amp; udtConfig.dwStartType
        List1.AddItem "Service Error Control: " &amp; udtConfig.dwErrorControl

        sTemp = Space(255)

        ' Now use the pointer obtained to copy the path into the temporary
        ' String Variable
        '
        lRet = lstrcpy(sTemp, udtConfig.lpBinaryPathName)
        List1.AddItem "Service Binary Path: " &amp; sTemp

        lRet = lstrcpy(sTemp, udtConfig.lpDependencies)
        List1.AddItem "Service Dependencies: " &amp; sTemp

        lRet = lstrcpy(sTemp, udtConfig.lpDisplayName)
        List1.AddItem "Service DisplayName: " &amp; sTemp

        lRet = lstrcpy(sTemp, udtConfig.lpLoadOrderGroup)
        List1.AddItem "Service LoadOrderGroup: " &amp; sTemp

        lRet = lstrcpy(sTemp, udtConfig.lpServiceStartName)
        List1.AddItem "Service Start Name: " &amp; sTemp

     CloseHandles:
     ' Close the Handle to the Service
     '
        CloseServiceHandle (hSVC)

     ' Close the Handle to the Service Control Manager
     '
        CloseServiceHandle (hSCM)

     End Sub

5. Run the sample and type the Service Name in the Text box. For this example
  type in "Eventlog" without the quotes.

  NOTE: This is not necessarily the same as the "Display Name" that shows in the
  Services applet of the Control Panel. The Service names registered on the
  system can be located under the following registry key:

  HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services

6. Click the CommandButton.

  The results displayed in the list box should be similar to the following:

     Service Status : The Service is Running
     Service Type: 32
     Service Start Type: 2
     Service Error Control: 1
     Service Binary Path: C:\WINNT\system32\services.exe
     Service Dependencies:

     Service DisplayName: EventLog
     Service LoadOrderGroup: Event log
     Service Start Name: LocalSystem

Additional query words:

======================================================================
Keywords          : kbOSWinNT kbOSWin2000 kbRegistry kbVBp kbVBp500 kbVBp600 kbGrpDSVB kbDSupport 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB500Search kbVB600Search kbVBA500Search kbVBA500 kbVBA600 kbVB500 kbVB600 kbZNotKeyword3
Version           : WINDOWS:5.0,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>