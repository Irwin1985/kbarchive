<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q217082: PRB: Connection Busy Message w/Remote Views of SQL Server Tables | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q217082: PRB: Connection Busy Message w/Remote Views of SQL Server Tables" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/217/Q217082/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/217/Q217082/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q217082: PRB: Connection Busy Message w/Remote Views of SQL Server Tables","url":"http://jeffpar.github.io/kbarchive/kb/217/Q217082/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q217082-prb-connection-busy-message-wremote-views-of-sql-server-tables">Q217082: PRB: Connection Busy Message w/Remote Views of SQL Server Tables</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q217082
Product(s): Microsoft FoxPro
Version(s): WINDOWS:2.5,3.0,3.0b,5.0,5.0a,6.0
Operating System(s): 
Keyword(s): kbSQL kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbMDAC250 kbSQLProg
Last Modified: 27-JUL-2001

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 5.0a, 6.0 
- Microsoft Data Access Components version 2.5 
-------------------------------------------------------------------------------

SYMPTOMS
========

When attempting to open a Remote View using a shared connection to access SQL
Server tables, the following error message may appear:

  Connection "connection name" is busy

RESOLUTION
==========

Set the FETCHSIZE property of remote views using shared connections to a value
of -1.

  OPEN DATABASE MYDATA
  =DBSETPROP('MyView','View','FetchSize',-1)

-or-

  OPEN DATABASE MYDATA
  USE MyView IN 0
  SELECT MyView
  =CURSORSETPROP('FetchSize',-1)

Setting the FetchSize Property of the remote view to -1 causes Visual FoxPro to
retrieve the complete result set (within the limits set by the MaxRecords
setting).

MORE INFORMATION
================

The default FetchSize Property value for remote views is 100.

The error message indicates that the shared remote connection is in use by
another executing statement. This behavior occurs when multiple remote views
have been defined with the following characteristics:

- Using a shared remote connection to the server.

- Accessing the same base table(s).

- The number of records being returned is greater than or equal to the
  FETCHSIZE property of the view.

Steps to Reproduce Behavior
---------------------------

1. From the Windows Control Panel, open ODBC Data Source Administrator, and add
  a DSN named MyDSN, using the SQL Server ODBC Driver.

2. Create a program file named "REMOVIEW.PRG" using the following code: (NOTE:
  This program will create a new SQL Server table in the database that the DSN
  specifies as the default, and necessitates appropriate permissions.)

  lcsafestat=SET('SAFETY')
  lcexclstat=SET('EXCLUSIVE')
  lcMultiLocks=SET('MULTILOCKS')
  SET SAFETY OFF
  SET EXCLUSIVE ON
  SET MULTILOCKS ON
  gnConnHandle=SQLCONNECT('MyDSN','sa','')
  lnCreate=0
  lnUseDB=0
  IF gnConnHandle &gt; 0
     * Use the SQL Server PUBS database
     SQLCommand="USE PUBS"
     lnUseDB=sqlexec(gnConnHandle,SQLCommand)
     IF lnUseDB &gt; 0
        * Create table TEST_A in the PUBS database
        SQLCommand="CREATE TABLE TEST_A (VAR1 CHAR(10) NULL, " + ;
           "VAR2 VARCHAR(20) NULL)"
        lnCreate=sqlexec(gnConnHandle,SQLCommand)
     ENDIF
     * Disconnect from SQL Server
     =sqldisconn(gnConnHandle)
  ENDIF
  IF lnCreate &gt; 0
     * Create a Visual FoxPro database named TESTA
     CREATE DATABASE testa
     * Create a remote connection to SQL Server
     CREATE CONNECTION test1 DATASOURCE 'MyDSN' USERID 'SA' PASSWORD '' ;
        DATABASE 'PUBS'
     * Create three remote views to SQL Server
     IF lnUseDB &gt; 0
        CREATE SQL VIEW testa REMOTE CONNECTION test1 SHARE ;
           AS SELECT * FROM dbo.test_a test_a
        CREATE SQL VIEW testb REMOTE CONNECTION test1 SHARE ;
           AS SELECT * FROM dbo.test_a test_a
        CREATE SQL VIEW testc REMOTE CONNECTION test1 SHARE ;
           AS SELECT * FROM dbo.test_a test_a
     ENDIF
     * Set Properties for the three remote views
     FOR i=1 TO 3
        ViewName="TEST"+CHR(64+i)
        =DBSETPROP(ViewName,'View','UpdateType',1)
        =DBSETPROP(ViewName,'View','WhereType',3)
        =DBSETPROP(ViewName,'View','UseMemoSize',255)
        =DBSETPROP(ViewName,'View','FetchSize',100)
        =DBSETPROP(ViewName,'View','MaxRecords',-1)
        =DBSETPROP(ViewName,'View','Tables','dbo.test_a')
        =DBSETPROP(ViewName,'View','Prepared',.T.)
        =DBSETPROP(ViewName,'View','FetchMemo',.T.)
        =DBSETPROP(ViewName,'View','SendUpdates',.T.)
        =DBSETPROP(ViewName,'View','BatchUpdateCount',1)
        =DBSETPROP(ViewName,'View','CompareMemo',.T.)
        =DBSETPROP(ViewName,'View','FetchAsNeeded',.F.)
        =DBSETPROP(ViewName,'View','ShareConnection',.T.)
     NEXT
     USE testa
     testb=AFIELDS(viewflds)
     FOR i=1 TO ALEN(viewflds,1)
        fld_name=viewflds[i,1]
        viewfield='TESTA'+"."+ALLTRIM(fld_name)
        IF i=1
           =DBSETPROP(viewfield,'Field','KeyField',.T.)
        ELSE
           =DBSETPROP(viewfield,'Field','KeyField',.F.)
        ENDIF
        =DBSETPROP(viewfield,'Field','Updatable',.T.)
        =DBSETPROP(viewfield,'Field','UpdateName',"dbo.test_a" + ;
           "."+ALLTRIM(fld_name))
     NEXT
     * Insert 100 records into the base table via remote view TESTA
     FOR j=1 TO 100
        INSERT INTO testa VALUES ; 
           (REPLICATE('0',6)+ALLTRIM(STR(j)),TTOC(DATETIME()+j))
     NEXT
     =TABLEUPDATE(.T.)
     SELECT testa
     USE testb IN 0
     * Get a Connection 'TEST1' is Busy message on next line
     * Select Ignore.
     USE testc IN 0
     CLOSE ALL
     gnConnHandle=SQLCONNECT('MyDSN','sa','')
     lnUseDB=0
     IF gnConnHandle &gt; 0
        * Use the SQL Server PUBS database
        SQLCommand="USE PUBS"
        lnUseDB=sqlexec(gnConnHandle,SQLCommand)
        IF lnUseDB &gt; 0
           * Drop Table TEST_A from the PUBS database
           SQLCommand="DROP TABLE TEST_A"
           =sqlexec(gnConnHandle,SQLCommand)
        ENDIF
        =sqldisconn(gnConnHandle)
     ENDIF
  ELSE
     =MESSAGEBOX('Unable to Create SQL Server Table',16)
  ENDIF
  SET SAFETY &amp;lcsafestat
  SET EXCLUSIVE &amp;lcexclstat
  SET MULTILOCKS &amp;lcMultiLocks
  RETURN

3. From the Command window, type "DO REMOVIEW," and observe that the program
  runs with no error messages.

4. From the Command window, type "DO REMOVIEW," and observe that an error occurs
  when the program attempts to open the view named TESTC.

5. When the error message appears, select the Ignore option.

6. Change the following line of code:

  =DBSETPROP(ViewName,'View','FetchSize',100)

to read as follows:

  =DBSETPROP(ViewName,'View','FetchSize',-1)

7. From the Command window, type "DO REMOVIEW," and observe that the program
  runs with no error messages.

REFERENCES
==========

For additional information about Remote Views, please see the following articles
in the Microsoft Knowledge Base:

Q137944 How to Create a Remote Connection or View Programmatically

Q174807 HOWTO: Edit the SQL Statement of a Remote or Local View

Q191343 PRB: Connection Busy Error with a Shared Connection

Q191344 PRB: Base Table Fields Changed with Remote View

(c) Microsoft Corporation 1999, All Rights Reserved. Contributions by John Desch,
Microsoft Corporation.


Additional query words: KBDSE

======================================================================
Keywords          : kbSQL kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbMDAC250 kbSQLProg 
Technology        : kbVFPsearch kbAudDeveloper kbMDACSearch kbMDAC250 kbVFP300 kbVFP300b kbVFP500 kbVFP600 kbVFP500a
Version           : WINDOWS:2.5,3.0,3.0b,5.0,5.0a,6.0
Issue type        : kbprb

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>