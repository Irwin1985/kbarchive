<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q217012: HOWTO: Format Strings to Right-Justify When Printing | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q217012: HOWTO: Format Strings to Right-Justify When Printing" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/217/Q217012/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/217/Q217012/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q217012: HOWTO: Format Strings to Right-Justify When Printing","url":"http://jeffpar.github.io/kbarchive/kb/217/Q217012/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q217012-howto-format-strings-to-right-justify-when-printing">Q217012: HOWTO: Format Strings to Right-Justify When Printing</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q217012
Product(s): Microsoft Visual Basic for Windows
Version(s): WINDOWS:5.0,6.0
Operating System(s): 
Keyword(s): kbString kbVBp kbVBp500 kbVBp600 kbGrpDSVB kbDSupport
Last Modified: 11-JAN-2001

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Learning Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Professional Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Enterprise Edition for Windows, versions 5.0, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

There are several different ways to right-justify strings using the Format
function:

- Use the @ character.

- Use the RSet function.

- Use workarounds with the Format$ function.

MORE INFORMATION
================

Using the @ character::

NOTE: This technique is only effective with monospace fonts, such as Courier
New.

1. Format the number into a string with numeric conversion characters, for
  example, $##0.00.

2. Format the resulting string with a format string consisting of a number of @
  characters equal in length to the desired format, for example, @@@@@@@.

The following code sample formats several numbers using seven @ characters and a
seven character format, $##0.00.

     Print "|" &amp; Format$(Format$(1.5, "$##0.00"), "@@@@@@@") &amp; "|"
     Print "|" &amp; Format$(Format$(12.5, "$##0.00"), "@@@@@@@") &amp; "|"
     Print "|" &amp; Format$(Format$(123.5, "$##0.00"), "@@@@@@@") &amp; "|"

The output is;

|  $1.50|
| $12.50|
|$123.50|

Using the RSet function::

When used in conjunction with RSet, the format function works on fixed length
strings. The following code sample illustrates the use of RSet:

     x = (Format$(123.5, "$##0.00"))
     Print "x" &amp; x &amp; "x"
     RSet x = (Format$(1.5, "$##0.00"))
     Print "x" &amp; x &amp; "x"

The output is:

x$123.50x 
x  $1.50x

Workarounds using the Format$ function::

NOTE: These techniques are only effective with monospace fonts, such as Courier
New.

The Format$ function does not right-justify strings when used with the # symbol.
The first code sample uses the Len function to determine how many spaces need to
be added to the left of the string representing the number, in order to right
justify the string:

     required = 8 ' longest number expected
     a = 1.23
     b = 44.56
     num1$ = Format$(a, "#0.00") ' this converts the number to a string
     num2$ = Format$(b, "#0.00") ' with 2 decimal places and a leading zero
     'Debug.Print num2$
     If (required - Len(num1$)) &gt; 0 Then
     num1$ = Space$(required - Len(num1$)) + num1$
     End If
     
     If (required - Len(num2$)) &gt; 0 Then
     num2$ = Space$(required - Len(num2$)) + num2$
     End If
  ' test output
     Print num1$
     Print num2$

The output is:

  1.23
 44.56

The second Format$ sample is reprinted with the permission of its author, Karl
Peterson. His LPad function uses the Right$ function:

  Private Function LPad(ValIn As Variant, nDec As Integer, _
                        WidthOut As Integer) As String
  '
  ' Formatting function left pads with spaces, using specified
  ' number of decimal digits.
  '
     If IsNumeric(ValIn) Then
        If nDec &gt; 0 Then
           LPad = Right$(Space$(WidthOut) &amp; _
                  Format$(ValIn, "0." &amp; String$(nDec, "0")), _
                  WidthOut)
        Else
           LPad = Right$(Space$(WidthOut) &amp; Format$(ValIn, "0"), WidthOut)
        End If
     Else
        LPad = Right$(Space$(WidthOut) &amp; ValIn, WidthOut)
     End If
  End Function

Step by Step Sample:

1. Start a new Visual Basic Standard EXE project. Form1 is created by default.

2. Add four CommandButton controls to Form1. Position them to the far right of
  the form window.

3. Add the following code to the General Declarations section of Form1:

  Option Explicit

  Private Sub Command1_Click()
     Me.Print "|" &amp; Format$(Format$(1.5, "$##0.00"), "@@@@@@@") &amp; "|"
     Me.Print "|" &amp; Format$(Format$(12.5, "$##0.00"), "@@@@@@@") &amp; "|"
     Me.Print "|" &amp; Format$(Format$(123.5, "$##0.00"), "@@@@@@@") &amp; "|"
  End Sub

  Private Sub Command2_Click()
     Dim x As String
     x = (Format$(123.5, "$##0.00"))
     Me.Print "x" &amp; x &amp; "x"
     RSet x = (Format$(1.5, "$##0.00"))
     Me.Print "x" &amp; x &amp; "x"
  End Sub

  Private Sub Command3_Click()
     Dim required As Integer
     Dim a As Single
     Dim b As Single
     Dim num1$, num2$

     required = 8 ' longest number expected
     a = 1.23
     b = 44.56
     num1$ = Format$(a, "#0.00") ' this converts the number to a string
     num2$ = Format$(b, "#0.00") ' with two decimal places and a leading zero
     'Debug.Print num2$
     If (required - Len(num1$)) &gt; 0 Then
        num1$ = Space$(required - Len(num1$)) &amp; num1$
     End If
     
     If (required - Len(num2$)) &gt; 0 Then
        num2$ = Space$(required - Len(num2$)) &amp; num2$
     End If
  ' test output
     Me.Print num1$
     Me.Print num2$
  End Sub

  Private Sub Command4_Click()
     Dim xstring As String
     xstring = LPad(2.3, 2, 7)
     Me.Print "K" &amp; xstring &amp; "K"
  End Sub

  Private Sub Form_Load()
     Command1.Caption = "@"
     Command1.Font.Size = 18
     Command2.Caption = "Rset"
     Command3.Caption = "Format$"
     Command4.Caption = "VBPJ"
     Me.Font.Name = "Courier New"
  End Sub

  Private Function LPad(ValIn As Variant, nDec As Integer, _
                        WidthOut As Integer) As String
  '
  ' Formatting function left pads with spaces, using specified
  ' number of decimal digits.
  '
     If IsNumeric(ValIn) Then
        If nDec &gt; 0 Then
           LPad = Right$(Space$(WidthOut) &amp; _
                  Format$(ValIn, "0." &amp; String$(nDec, "0")), _
                  WidthOut)
        Else
           LPad = Right$(Space$(WidthOut) &amp; Format$(ValIn, "0"), WidthOut)
        End If
     Else
        LPad = Right$(Space$(WidthOut) &amp; ValIn, WidthOut)
     End If
  End Function

4. Run the program, click the command buttons, and observe the results.

REFERENCES
==========

  Q95945 How to Right Justify Numbers Using Format$

  Q79094 PRB: Format$ Using # for Digit Affects Right Alignment

Additional query words: right-justify align alignment column

======================================================================
Keywords          : kbString kbVBp kbVBp500 kbVBp600 kbGrpDSVB kbDSupport 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB500Search kbVB600Search kbVBA500 kbVBA600 kbVB500 kbVB600
Version           : WINDOWS:5.0,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>