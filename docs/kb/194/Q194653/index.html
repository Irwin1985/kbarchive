<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q194653: FIX: MSHFLexGrid Does Not Display More Than 2048 Rows | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q194653: FIX: MSHFLexGrid Does Not Display More Than 2048 Rows" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/194/Q194653/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/194/Q194653/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q194653: FIX: MSHFLexGrid Does Not Display More Than 2048 Rows","url":"http://jeffpar.github.io/kbarchive/kb/194/Q194653/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q194653-fix-mshflexgrid-does-not-display-more-than-2048-rows">Q194653: FIX: MSHFLexGrid Does Not Display More Than 2048 Rows</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q194653
Product(s): Microsoft Visual Basic for Windows
Version(s): WINDOWS:6.0
Operating System(s): 
Keyword(s): kbVBp600bug kbVS600sp3fix
Last Modified: 30-MAY-1999

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Enterprise Edition for Windows, version 6.0 
-------------------------------------------------------------------------------

SYMPTOMS
========

The Hierarchical FlexGrid control always displays a maximum of 2048 rows
regardless of the number of records in the data source.

RESOLUTION
==========

Although the record-count of the mshflexgrid is correct, only the first 2048
records are displayed. If you need to display more than 2048, you need to open
the recordset and populate the grid using the GetString method of ADO and the
Clip property of the MSHFlexGrid.

The code below can be used in place of the code in the Command1_Click event in
the MORE INFORMATION section to work around this behavior:

     Dim cn As New ADODB.Connection
     Dim rs As New ADODB.Recordset
     Dim rsVar As Variant
     Dim i As Integer
     cn.Open "Testing"  '&lt;-- Your DSN

     rs.Open "select * from Cies", cn, adOpenStatic, adLockOptimistic
     rs.MoveLast

     rs.MoveFirst
     ' Assuming that rs is your ADO recordset
     MSHFlexGrid1.Rows = rs.RecordCount + 1

     rsVar = rs.GetString(adClipString, rs.RecordCount)

     MSHFlexGrid1.Cols = rs.Fields.Count

     ' Set column names in the grid
     For i = 0 To rs.Fields.Count - 1

         MSHFlexGrid1.TextMatrix(0, i) = rs.Fields(i).Name

     Next

     MSHFlexGrid1.Row = 1
     MSHFlexGrid1.Col = 0

     ' Set range of cells in the grid
     MSHFlexGrid1.RowSel = MSHFlexGrid1.Rows - 1
     MSHFlexGrid1.ColSel = MSHFlexGrid1.Cols - 1
     MSHFlexGrid1.Clip = rsVar

     ' Reset the grid's selected range of cells
     MSHFlexGrid1.RowSel = MSHFlexGrid1.Row
     MSHFlexGrid1.ColSel = MSHFlexGrid1.Col

     rs.Close
     Set rs = Nothing
     cn.Close
     Set cn = Nothing

STATUS
======

Microsoft has confirmed this to be a bug in the Microsoft products listed at the
beginning of this article.

This bug was corrected in Visual Studio 6.0 Service Pack 3.

For more information about Visual Studio service packs, please see the following
articles in the Microsoft Knowledge Base:

Q194022 INFO: Visual Studio 6.0 Service Packs, What, Where, Why

Q194295 HOWTO: Tell That Visual Studio 6.0 Service Packs Are Installed

MORE INFORMATION
================

This behavior of the MSHFlexGrid can be demonstrated against various back- ends
including SQL Server, Access, etc.

Steps to Reproduce Behavior
---------------------------

Test Table
----------

1. Create a simple table with the following fields:

    Name         Data Type (SQL Server)       Data Type(Access) 
 ------------------------------------------------------------------

    PartID          Integer                      Integer
    PartName        VarChar (10)                 Text (10)

2. Insert about 3000 - 10000 rows in your table. For simplicity, you can create
  a loop and insert the same record in a counter of 3000 to 10000.

3. Save your table as "Test."

  NOTE: You can choose any other back-end to perform the test.

Visual Basic Client
-------------------

1. Start a new Standard EXE project in Visual Basic. Form1 is created by
  default.

2. Select "Components" on the Project menu, and choose the following components:

   - Microsoft ADO Data Control 6.0 (OLE DB).
   - Microsoft Hierarchical FlexGrid Control 6.0.

3. Select References on the Project menu, and click "Microsoft ActiveX Data
  Objects 2.0 Library."

4. Place the following objects on the form:

   Control             Name                  Caption
  -------------------------------------------------------------------

  CommandButton       Command1           Using ADO Recordset
  CommandButton       Command2           Using ADODC
  CommandButton       Command3           Free MSHFlexGrid
  MSHFlexGrid         MSHFlexGrid1       Testing MSHFlexGrid Display
  ADODC               Adodc1

5. Bind ADODC via your DSN or via your native OLEDB provider (if it exists). In
  the RecordSource property, write: Select * From Test.

6. Place the following code in the General Declaration section of Form1:

         Option Explicit

         Private Sub Command1_Click()

            Dim cn As New ADODB.Connection
            Dim rs As New ADODB.Recordset

            cn.Open "Testing"               '&lt;-- Your DSN

            rs.Open "select * from Test", cn, adOpenStatic, adLockOptimistic
            rs.MoveLast

            ' Check the number records in your ADO recordset
            ' The RecordCount property of the ADO recordset reflects the
            ' actual number of records

            Debug.Print "ADO recordset record-count: " &amp; vbTab &amp; _
                rs.RecordCount

            Form1.Caption = "Loading grid... please wait..."

            Set MSHFlexGrid1.Recordset = rs

            ' Check the number of rows that the Hierarchical FlexGrid
            ' contains The RecordCount property of the Hierarchical
            ' FlexGrid also reflects the actual number of records

            Debug.Print "MSHFlexGrid1 Recordset record-count: " &amp; vbTab &amp;  _
               MSHFlexGrid1.Recordset.RecordCount

            Form1.Caption = "MSHFlexGrid Display Sample"

            ' Free resources
            rs.Close
            Set rs = Nothing
            cn.Close
            Set cn = Nothing

         End Sub

         Private Sub Command2_Click()

            Form1.Caption = "Loading data... please wait..."

            ' Set the Grid's source to be ADODC
            Set MSHFlexGrid1.DataSource = Adodc1

            ' Check the number of records in ADODC recordset
            Debug.Print "ADO data control record-count: " &amp; vbTab &amp;  _
               Adodc1.Recordset.RecordCount
            Form1.Caption = "MSHFlexGrid Display Sample"
         End Sub

         Private Sub Command3_Click()
            MSHFlexGrid1.Clear
            Form1.Caption = "Grid is clear now..."
         End Sub

7. Press the F5 key to run the code, and note that only 2048 records are
  displayed in the MSHFlexGrid.

  NOTE: Connecting to the data source using the OLEDB Provider for ODBC or using
  the native OLEDB Provider has no effect on the Hierarchical FlexGrid
  behavior.

Additional query words:

======================================================================
Keywords          : kbVBp600bug kbVS600sp3fix 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB600Search kbVB600
Version           : WINDOWS:6.0
Issue type        : kbbug
Solution Type     : kbfix

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>