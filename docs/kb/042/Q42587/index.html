<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q42587: QuickBASIC Program to Change the Mouse Shape in Graphics Mode | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q42587: QuickBASIC Program to Change the Mouse Shape in Graphics Mode" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/042/Q42587/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/042/Q42587/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q42587: QuickBASIC Program to Change the Mouse Shape in Graphics Mode","url":"http://jeffpar.github.io/kbarchive/kb/042/Q42587/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q42587-quickbasic-program-to-change-the-mouse-shape-in-graphics-mode">Q42587: QuickBASIC Program to Change the Mouse Shape in Graphics Mode</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q42587
Product(s): See article
Version(s): 4.00 4.00b 4.50
Operating System(s): MS-DOS
Keyword(s): ENDUSER | SR# S890222-68 B_BasicCom | mspl13_basic
Last Modified: 12-DEC-1989

This information applies to QuickBASIC Versions 4.00, 4.00b, and 4.50,
Microsoft BASIC Compiler Versions 6.00 and 6.00b for MS-DOS, and
Microsoft BASIC PDS Version 7.00 for MS-DOS.

The code shown below demonstrates a method of creating a mouse cursor
of a different shape when in a graphics mode. This example shape is
taken from Page 7-31 of the "Microsoft Mouse Programmer's Reference
Guide" and requires an IBM EGA or IBM VGA or compatible graphics
adapter.

The sample BASIC code on Page 6-15 of the same manual is not
appropriate for QuickBASIC or the BASIC compiler. The following
changes must be made:

1. The array "CURSOR" must be dimensioned.

2. The array "CURSOR" must be in a COMMON block.

3. The fourth parameter passed to MOUSE on line 4500 must be
   VARPTR(CURSOR(0,0))

When using BASIC PDS 7.00 it is also possible to use the MOUSEDRIVER
SUBprogram in the MOUSE.BAS portion of the User Interface Toolbox
(rather then the CALL INTERRUPT method shown below) to change the
mouse pointer shape.

Code Example
------------

DECLARE SUB mouse (m1%, m2%, m3%, m4%)
DECLARE FUNCTION MouseInit% ()
DECLARE SUB MouseShow ()
DECLARE SUB MouseButPos (ButStat%, CurHor%, CurVert%)

' The following $INCLUDE filename would be 'QBX.BI' if you
' are using the BASIC PDS 7.00:
' $INCLUDE: 'qb.bi'

DEFINT A-Z
DIM cursor(16, 2)
COMMON cursor()
' Data for an HOUR Glass shape
'Array Screen Mask             binary
Cursor(0, 0) = &amp;H07E0         '0000011111100000
Cursor(1, 0) = &amp;H0180         '0000000110000000
Cursor(2, 0) = &amp;H0000         '0000000000000000
Cursor(3, 0) = &amp;HC003         '1100000000000011
Cursor(4, 0) = &amp;HF00F         '1111000000001111
Cursor(5, 0) = &amp;HC003         '1100000000000011
Cursor(6, 0) = &amp;H0000         '0000000000000000
Cursor(7, 0) = &amp;H0180         '0000000110000000
Cursor(8, 0) = &amp;H07E0         '0000011111100000
Cursor(9, 0) = &amp;HFFFF         '1111111111111111
Cursor(10 0) = &amp;HFFFF         '1111111111111111
Cursor(11, 0) = &amp;HFFFF        '1111111111111111
Cursor(12, 0) = &amp;HFFFF        '1111111111111111
Cursor(13, 0) = &amp;HFFFF        '1111111111111111
Cursor(14, 0) = &amp;HFFFF        '1111111111111111
Cursor(15, 0) = &amp;HFFFF        '1111111111111111

' Cursor mask                 binary
Cursor(0, 1) = &amp;H0000         '0000000000000000
Cursor(1, 1) = &amp;H700E         '0111000000001110
Cursor(2, 1) = &amp;H1C38         '0001110000111000
Cursor(3, 1) = &amp;H0660         '0000011001100000
Cursor(4, 1) = &amp;H03C0         '0000001111000000
Cursor(5, 1) = &amp;H0660         '0000011001100000
Cursor(6, 1) = &amp;H1C38         '0001110000111000
Cursor(7, 1) = &amp;H700E         '0111000000001110
Cursor(8, 1) = &amp;H0000         '0000000000000000
Cursor(9, 1) = &amp;H0000         '0000000000000000
Cursor(10, 1) = &amp;H0000        '0000000000000000
Cursor(11, 1) = &amp;H0000        '0000000000000000
Cursor(12, 1) = &amp;H0000        '0000000000000000
Cursor(13, 1) = &amp;H0000        '0000000000000000
Cursor(14, 1) = &amp;H0000        '0000000000000000
Cursor(15, 1) = &amp;H0000        '0000000000000000

Button$(0) = "Left Up   / Right Up  "
Button$(1) = "Left Down / Right Up  "
Button$(2) = "Left Up   / Right Down"
Button$(3) = "Left Down / Right Down"

SCREEN 9
' this part initializes the mouse
IF MouseInit = 0 THEN
  PRINT "can not initialize mouse"
  END
END IF

' this is the part that makes the shape
m1 = 9
m2 = 7
m3 = 7
m4 = VARPTR(cursor(0, 0))
CALL mouse(m1, m2, m3, m4)

' this call turns the mouse cursor on
MouseShow

LOCATE 10, 10: PRINT "Button Status :";
LOCATE 11, 10: PRINT "Horizontal pos :";
LOCATE 12, 10: PRINT "Vertical pos  :";

WHILE INKEY$ = ""
CALL MouseButPos(ButStat, CurHor, CurVert)

LOCATE 10, 25: PRINT Button$(ButStat)
LOCATE 11, 25: PRINT CurHor
LOCATE 12, 25: PRINT CurVert
WEND

SUB mouse (m1, m2, m3, m4)
  DIM InRegs AS RegType
  InRegs.ax = m1
  InRegs.bx = m2
  InRegs.cx = m3
  InRegs.dx = m4
  CALL INTERRUPT(51, InRegs, InRegs)
  m1 = InRegs.ax
  m2 = InRegs.bx
  m3 = InRegs.cx
  m4 = InRegs.dx

END SUB

SUB MouseButPos (ButStat, CurHor, CurVert)
  m1 = 3
  CALL mouse(m1, m2, m3, m4)
  ButStat = m2
  CurHor = m3
  CurVert = m4
END SUB

FUNCTION MouseInit
   m1 = 0
   CALL mouse(m1, m2, m3, m4)
   MouseInit = m1

END FUNCTION

SUB MouseShow
  m1 = 1
  CALL mouse(m1, m2, m3, m4)

END SUB
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>