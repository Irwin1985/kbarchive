<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q157854: HOWTO: Create a New Form Based on a Template | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q157854: HOWTO: Create a New Form Based on a Template" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/157/Q157854/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/157/Q157854/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q157854: HOWTO: Create a New Form Based on a Template","url":"http://jeffpar.github.io/kbarchive/kb/157/Q157854/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q157854-howto-create-a-new-form-based-on-a-template">Q157854: HOWTO: Create a New Form Based on a Template</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q157854
Product(s): Microsoft FoxPro
Version(s): WINDOWS:3.0,3.0b,5.0,6.0
Operating System(s): 
Keyword(s): kbnokeyword kbvfp300 kbvfp500 kbvfp600
Last Modified: 11-DEC-1999

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

The following program creates a new form (SCX) from a template without using the
Options dialog box setting (accessed from the Tools menu).

MORE INFORMATION
================

1. Create a program called Newscx.prg as follows:

        LPARAMETERS tcFileName,tcClass,tcClassLibrary
        LOCAL lcFileName,lcLastSetClassLib,oForm

        IF EMPTY(tcFileName)
           CREATE FORM "" NOWAIT
           RETURN
        ENDIF
        lcFileName=ALLTRIM(tcFileName)
        IF NOT "."$lcFileName
           lcFileName=lcFileName+".SCX"
        ENDIF
        lcFileName=FULLPATH(lcFileName)
        IF FILE(lcFileName) AND MESSAGEBOX(LOWER(lcFileName)+;
           [ already exists.]+CHR(13)+;
           [Do you want to replace it?],52+256)#6
           RETURN .F.
        ENDIF
        IF EMPTY(tcClass)
           CREATE FORM (lcFileName) NOWAIT
           RETURN
        ENDIF
        IF EMPTY(tcClassLibrary)
           oForm=CREATEOBJECT(tcClass)
           IF TYPE("oForm")#"O"
              RETURN .F.
           ENDIF
           SET SAFETY OFF  &amp;&amp; Stop Save As dialog from appearing on next line
           oForm.SaveAs(lcFileName)
           SET SAFETY ON
           IF NOT FILE(lcFileName)
              RETURN .F.
           ENDIF
           MODIFY FORM (lcFileName) NOWAIT
           RETURN
        ENDIF
        lcLastSetClassLib=SET("CLASSLIB")
        SET CLASSLIB TO (tcClassLibrary) ADDITIVE
        oForm=CREATEOBJECT(tcClass)
        IF NOT lcLastSetClassLib==SET("CLASSLIB")
           RELEASE CLASSLIB (tcClassLibrary)
        ENDIF
        IF TYPE("oForm")#"O"
           RETURN .F.
        ENDIF
        SET SAFETY OFF    &amp;&amp; Stop Save As dialog from appearing on next line
        oForm.SaveAs(lcFileName)
        SET SAFETY ON
        IF NOT FILE(lcFileName)
           RETURN .F.
        ENDIF
        MODIFY FORM (lcFileName) NOWAIT
        RETURN

2. Run the program from the command line, passing it parameters for the name of
  the new form, the name of the form class on which the new form is based, and
  the name of the class library where the form class is stored. This assumes a
  class library containing at least one custom form class has already been
  created in the current directory. For example, to create a new form based on
  the "myform" class stored in a visual class library (VCX) called "myvcx,"
  issue the following command from the command line:

        =NewSCX("newform","myform","myvcx")

3. The preceding step opens the "newform" in the Form Designer. Edit the form in
  the Form Designer as usual.


Additional query words:

======================================================================
Keywords          : kbnokeyword kbvfp300 kbvfp500 kbvfp600 
Technology        : kbVFPsearch kbAudDeveloper kbVFP300 kbVFP300b kbVFP500 kbVFP600
Version           : WINDOWS:3.0,3.0b,5.0,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>