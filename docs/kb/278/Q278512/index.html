<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q278512: BUG: Visual FoxPro May Ignore DDE Messages | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q278512: BUG: Visual FoxPro May Ignore DDE Messages" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/278/Q278512/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/278/Q278512/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q278512: BUG: Visual FoxPro May Ignore DDE Messages","url":"http://jeffpar.github.io/kbarchive/kb/278/Q278512/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q278512-bug-visual-foxpro-may-ignore-dde-messages">Q278512: BUG: Visual FoxPro May Ignore DDE Messages</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q278512
Product(s): Microsoft FoxPro
Version(s): 5.0,5.0a,6.0
Operating System(s): 
Keyword(s): kbvfp300 kbvfp300b kbvfp300BUG kbvfp500 kbvfp500a kbvfp500aBUG kbvfp500bug kbvfp600 kbG
Last Modified: 04-APR-2001

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 5.0, 5.0a, 6.0 
-------------------------------------------------------------------------------

SYMPTOMS
========

You are using DDEAdvise to receive messages. Under a variety of system loads,
Visual FoxPro (VFP) may ignore these DDEAdvise messages. The behavior does not
produce an error that warns the user that the message was dropped.

CAUSE
=====

Dynamic data exchange (DDE) is an older method for inter-application
communication, which has been supplanted by newer technologies.

RESOLUTION
==========

If possible, use a different technology than DDE, such as COM.

STATUS
======

Microsoft has confirmed this to be a bug in the Microsoft products listed at the
beginning of this article.

MORE INFORMATION
================

Steps to Reproduce Behavior
---------------------------

1. Create a directory to hold the files that you will create in the steps that
  follow.

2. Paste the following code in a file named DDEForm.prg:

  PUBLIC loForm

  loForm = CREATEOBJECT("DDEForm")
  loForm.Visible = .T.

  DEFINE CLASS DDEForm AS form

     Caption = "DDE Server"
     Name = "DDEForm"
     lDone = .F.
     nChannel = 0
     cItem = ""

     ADD OBJECT cmdStartConn AS commandbutton WITH ;
        AutoSize = .T., ;
        Top = 24, ;
        Left = 24, ;
        Height = 27, ;
        Width = 109, ;
        Caption = "Start Connection", ;
        Name = "cmdStartConn"

     ADD OBJECT cmdEndConn AS commandbutton WITH ;
        AutoSize = .T., ;
        Top = 108, ;
        Left = 24, ;
        Height = 27, ;
        Width = 106, ;
        Caption = "End Connection", ;
        Name = "cmdEndConn"

     ADD OBJECT cmdStartData AS commandbutton WITH ;
        Top = 24, ;
        Left = 156, ;
        Height = 27, ;
        Width = 84, ;
        Caption = "Start Data", ;
        Name = "cmdStartData"

     ADD OBJECT cmdEndData AS commandbutton WITH ;
        Top = 108, ;
        Left = 156, ;
        Height = 27, ;
        Width = 84, ;
        Caption = "End Data", ;
        Name = "cmdEndData"

     PROCEDURE QueryUnload
        This.ReleaseConn()
     ENDPROC

     PROCEDURE ReleaseConn
        =DDESetService("VFPServer", "release")
     ENDPROC
     
     PROCEDURE FastDoEvents
        *!* From Q268771 BUG: DOEVENTS Command Runs Slowly
        LOCAL lnRow, lnCol, lcWindow

        lcWindow = WONTOP()

        lnRow = MROW(lcWindow)
        lnCol = MCOL(lcWindow)

        IF ( lnRow &gt; 0 ) AND ( lnCol &gt; 0 )
           IF NOT EMPTY(lcWindow)
              MOUSE AT lnRow, lnCol WINDOW (lcWindow)
           ELSE
              MOUSE AT lnRow, lnCol 
           ENDif
        ELSE
           KEYBOARD " "
           =INKEY()
        ENDif

        DOEVENTS
     ENDPROC
     
     PROCEDURE cmdStartConn.Click
        =DDESetService("VFPServer", "define")
        =DDESetService("VFPServer", "advise", .T.)

        =DDESetTopic("VFPServer", "TestSequence", "cbSequence")
     ENDPROC

     PROCEDURE cmdEndConn.Click
        ThisForm.ReleaseConn()
     ENDPROC

     PROCEDURE cmdStartData.Click
        LOCAL i

        i = 0

        DO WHILE NOT ThisForm.lDone
           lnChannel = ThisForm.nChannel
           lcItem = ThisForm.cItem
           lcData = "Item " + LTRIM(STR(i, 10, 0))
           
           =DDEPoke(lnChannel, lcItem, lcData)
           
           i = i + 1

           ThisForm.FastDoEvents()  &amp;&amp; To allow cmdEndData to be clicked
        ENDdo
     ENDPROC

     PROCEDURE cmdEndData.Click
        ThisForm.lDone = .T.
     ENDPROC

  ENDDEFINE

3. Paste the following code in a file named cbSequence.prg:

  LPARAMETERS tnChannel, tcAction, tcItem, tcData, tcFormat, tnStatus

  WAIT WINDOW "Advising" NOWAIT
  _SCREEN.ActiveForm.nChannel = tnChannel
  _SCREEN.ActiveForm.cItem = tcItem

4. Create a table to log the results using the following code:

  CREATE TABLE SaveVal (cData C(10))

5. Paste the following code in a file named cbSaveIt.prg:

  LPARAMETERS tnChannel, tcAction, tcItem, tcData, tcFormat, tnStatus

  IF NOT USED("SaveVal")
  	USE SaveVal IN 0 SHARED
  ENDif

  INSERT INTO saveVal VALUES (tcData)

6. Run DDEForm.prg, and click Start Connection.

7. Start another instance of VFP, and SET DEFAULT TO the directory where the
  files are stored.

8. Type the following lines in the Command window:

  lnChannel = DDEInitiate("VFPServer", "TestSequence")
  ? DDEAdvise(lnChannel, "TestSequence", "cbSaveIt", 2)

9. Switch to the first instance, and click the Start Data button.

10. Switch to the second instance, and type the following code in the Command
  window:

  SET REFRESH TO 5,5
  BROWSE 

After a while, you should observe that during times of system load, numbers in
the sequence may be dropped without notice. If the load is heavier, more
messages will be dropped.

Additional query words:

======================================================================
Keywords          : kbvfp300 kbvfp300b kbvfp300BUG kbvfp500 kbvfp500a kbvfp500aBUG kbvfp500bug kbvfp600 kbGrpDSFox kbDSupport 
Technology        : kbVFPsearch kbAudDeveloper kbVFP500 kbVFP600 kbVFP500a
Version           : :5.0,5.0a,6.0
Issue type        : kbbug
Solution Type     : kbnofix

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>