<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q277725: HOWTO: Copy Fields Containing NULL to Text File | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q277725: HOWTO: Copy Fields Containing NULL to Text File" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/277/Q277725/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/277/Q277725/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q277725: HOWTO: Copy Fields Containing NULL to Text File","url":"http://jeffpar.github.io/kbarchive/kb/277/Q277725/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q277725-howto-copy-fields-containing-null-to-text-file">Q277725: HOWTO: Copy Fields Containing NULL to Text File</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q277725
Product(s): Microsoft FoxPro
Version(s): 5.0,5.0a,6.0
Operating System(s): 
Keyword(s): kbvfp500 kbvfp500a kbvfp600 kbXBase kbGrpDSFox kbDSupport kbCodeSnippet
Last Modified: 31-OCT-2000

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 5.0, 5.0a, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

The COPY TO command copies NULL fields as if they were empty. The reason for
this is that there is no way in ASCII to properly represent NULL. It is possible
that you will want to completely blank out fields (for example, "1,,3" instead
of "1,0,3") to represent nulls. If so, this code helps you do that.

MORE INFORMATION
================

This code represents one possible way to copy NULL fields to a text database.
Your method will depend on what the recipient is expecting; the code as written
here may not be suitable for your particular case.

The following code creates a test table.

  CREATE TABLE tblExport ;
     (cName C(10) NULL, ;
      nAge N(10, 2) NULL, ;
      lMarried L NULL, ;
      dAnniver D NULL, ;
      tBirth T NULL)
      
  INSERT INTO tblExport VALUES ("Record 1", 1, ;
     .T.,    {^2011/01/01}, .NULL.)
     
  INSERT INTO tblExport VALUES ("Record 2", 2, ;
     .F.,    .NULL.,        {^2002/02/02 02:02:02})
     
  INSERT INTO tblExport VALUES ("Record 3", 3, ;
     .NULL., {^2033/03/03}, {^2003/03/03 03:03:03})
     
  INSERT INTO tblExport VALUES ("Record 4", .NULL., ;
     .T.,    {^2044/04/04}, {^2004/04/04 04:04:04})
     
  INSERT INTO tblExport VALUES (.NULL., 5, ;
     .T.,    {^2055/05/05}, {^2005/05/05 05:05:05})

  *This is the actual conversion code.

  SET TEXTMERGE TO export.txt NOSHOW
  SET TEXTMERGE ON
   
  SCAN
     lcRecord = ''
     lnFieldCount = AFIELDS(laFields)
     llFirstField = .T.
     
     FOR i = 1 TO lnFieldCount
        lcFieldName = laFields(i, 1)
        lcFieldType = laFields(i, 2)
        lnFieldSize = laFields(i, 3)
        lnFieldDec  = laFields(i, 4)
        
        DO CASE
           CASE lcFieldType = "C"
              lcValue = '"' + RTRIM(EVAL(lcFieldName)) + '"'
           CASE lcFieldType = "N"
              lcValue = ;
                 LTRIM(STR(EVAL(lcFieldName), lnFieldSize, lnFieldDec))
           CASE lcFieldType = "L"
              IF ISNULL(EVAL(lcFieldName))
                 lcValue = .NULL.
              ELSE
                 lcValue = IIF(EVAL(lcFieldName), ".T.", ".F.")
              ENDif
           CASE lcFieldType = "D"
              lcValue = DTOC(EVAL(lcFieldName))
           CASE lcFieldType = "T"
              lcValue = TTOC(EVAL(lcFieldName))
        ENDcase
        lcValue = NVL(lcValue, '')
        
        IF EMPTY(lcRecord) AND llFirstField
           lcRecord = lcValue
        ELSE
           lcRecord = lcRecord + "," + lcValue
        ENDif
        
        llFirstField = .F.
     ENDfor

     *!* The following line does the actual output to file.
     \\&lt;&lt;lcRecord + CHR(13) + CHR(10)&gt;&gt;
  ENDscan

  SET TEXTMERGE TO

REFERENCES
==========

For additional information about COPY TO and NULL fields, click the article
number below to view the article in the Microsoft Knowledge Base:

  Q161054 FIX: COPY TO ... TYPE SDF Incorrectly Copies Null Fields

(c) Microsoft Corporation 2000, All Rights Reserved. Contributions by Garrett
Fitzgerald, Microsoft Corporation.


Additional query words:

======================================================================
Keywords          : kbvfp500 kbvfp500a kbvfp600 kbXBase kbGrpDSFox kbDSupport kbCodeSnippet 
Technology        : kbVFPsearch kbAudDeveloper kbVFP500 kbVFP600 kbVFP500a
Version           : :5.0,5.0a,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>