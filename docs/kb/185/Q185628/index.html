<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q185628: PRB: REPORT FORM TO PRINT Results in Incorrect Data | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q185628: PRB: REPORT FORM TO PRINT Results in Incorrect Data" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/185/Q185628/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/185/Q185628/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q185628: PRB: REPORT FORM TO PRINT Results in Incorrect Data","url":"http://jeffpar.github.io/kbarchive/kb/185/Q185628/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q185628-prb-report-form-to-print-results-in-incorrect-data">Q185628: PRB: REPORT FORM TO PRINT Results in Incorrect Data</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q185628
Product(s): Microsoft FoxPro
Version(s): MACINTOSH:3.0b; WINDOWS:3.0,3.0b,5.0,5.0a
Operating System(s): 
Keyword(s): 
Last Modified: 13-AUG-1999

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Macintosh, version 3.0b 
- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 5.0a 
-------------------------------------------------------------------------------

SYMPTOMS
========

When a REPORT FORM &lt;report name&gt; TO PRINTER command is issued and a grid
object has focus, incorrect data may be printed in the detail band of the
report.

CAUSE
=====

When the REPORT FORM command is issued from either a toolbar or a menu, while a
grid object has focus, the grid regains focus before the print job is completed.
When the grid regains focus, the currently selected workspace is set to the
record source associated with the grid.

RESOLUTION
==========

Here are three workarounds:

- Add a command button with print functions to the form containing the grid.

  -or-

- Create another form to accept the focus before printing. Here is a code
  example:

        oxprint=CREATEOBJECT(prtform)
        oxprint.SHOW

        DEFINE CLASS prtform AS FORM
           TOP = 50
           LEFT = 50
           HEIGHT = 2
           WIDTH = 2
           DOCREATE = .T.
           CAPTION = ""
           NAME = "Form2"

           PROCEDURE ACTIVATE
              * Insert REPORT FORM and any associated commands here.
              RELEASE THISFORM
           ENDPROC

        ENDDEFINE

  -or-

- Create a user-defined function (UDF) that sets the workarea and reference the
  UDF in the report. A code example is shown below:

        PROCEDURE SETWORKAREA
        IF USED('mytable')
           SELECT mytable
        ENDIF
        retval=ALIAS()
        RETURN retval

STATUS
======

Microsoft is researching this problem and will post new information here in the
Microsoft Knowledge Base as it becomes available.

MORE INFORMATION
================

Because the current workspace is effectively being reset to the grid's record
source, the report is run using the record source associated with the grid. This
behavior occurs even if the desired table or cursor is specified in the report's
DataEnvironment.

Steps to Reproduce Behavior
---------------------------

WARNING: Running the following code may result in the printing of large reports
spanning several pages.

1. Create a report, named Testgrid.frx, which contains the following fields:

        billings.line_no
        billings.unit_price
        billings.quantity

2. Save a copy of Testgrid.frx as Testwork.frx and add a field with the
  following expression:

        SETWORKAREA()

3. Create a program file named PRNDEMO, using the following code:

        CLOSE ALL
        OPEN DATABASE home()+"samples\data\testdata"
        USE orders IN 0
        ocusttool=CREATEOBJECT('toolbars')
        ocusttool.SHOW
        ocustomer=CREATEOBJECT('customer')
        ocustomer.SHOW
        READ EVENTS

        PROCEDURE sqlqry
        cporder=orders.order_id
        SELECT orditems.line_no, orditems.unit_price, ;
           orditems.quantity ;
           FROM orditems ;
           WHERE orditems.order_id=cporder ;
           ORDER BY orditems.line_no ;
           INTO CURSOR billings

        PROCEDURE prnform
        PARAMETER prnout
           DO sqlqry IN PRNDEMO
        DO CASE
           CASE prnout="PREVIEW"
              REPORT FORM testgrid PREVIEW
           CASE prnout="WORKAROUND1" ;
                 OR prnout="WORKAROUND2"
              REPORT FORM testgrid NOCONSOLE TO PRINT
           CASE prnout="WORKAROUND3"
              REPORT FORM testwork NOCONSOLE TO PRINT
           OTHERWISE
              REPORT FORM testgrid NOCONSOLE TO PRINT
        ENDCASE
        SELECT billings
        USE

        DEFINE CLASS prtform AS FORM
           TOP = 50
           LEFT = 50
           HEIGHT = 2
           WIDTH = 2
           DOCREATE = .T.
           CAPTION = ""
           NAME = "Form2"
           PROCEDURE ACTIVATE
              DO prnform IN PRNDEMO WITH "WORKAROUND2"
              RELEASE THISFORM
           ENDPROC
        ENDDEFINE

        DEFINE CLASS customer AS FORM
           TOP = 0
           LEFT = 0
           HEIGHT = 200
           WIDTH = 385
           DOCREATE = .T.
           CAPTION = "Click Form to Print Illustrate Problem"
           NAME = "Form1"
           ADD OBJECT grid1 AS GRID WITH ;
              COLUMNCOUNT = 12, ;
              HEIGHT = 72, ;
              LEFT = 25, ;
              RECORDSOURCE = "orders", ;
              RECORDSOURCETYPE = 1, ;
              TOP = 24, ;
              WIDTH = 335, ;
              NAME = "Grid1"
           ADD OBJECT command1 AS COMMANDBUTTON WITH ;
              LEFT=24, ;
              HEIGHT=100, ;
              CAPTION="Work Around \&lt;1", ;
              TOP=100, ;
              WIDTH=335
           PROCEDURE CLICK
              DO prnform IN PRNDEMO WITH ""
           ENDPROC
           PROCEDURE command1.CLICK
              DO prnform IN PRNDEMO WITH "WORKAROUND1"
           ENDPROC
        ENDDEFINE

        DEFINE CLASS toolbars AS TOOLBAR
           CAPTION = "Toolbar1"
           HEIGHT = 29
           LEFT = 0
           TOP = 0
           WIDTH = 250
           NAME = "toolbars"
           ADD OBJECT command1 AS COMMANDBUTTON WITH ;
              TOP = 4, ;
              LEFT = 6, ;
              HEIGHT = 22, ;
              WIDTH = 96, ;
              CAPTION = "\&lt;Preview", ;
              DEFAULT = .F., ;
              NAME = "Command1"
           ADD OBJECT command2 AS COMMANDBUTTON WITH ;
              TOP = 4, ;
              LEFT = 102, ;
              HEIGHT = 22, ;
              WIDTH = 96, ;
              CAPTION = "\&lt;Failing Print", ;
              DEFAULT = .F., ;
              NAME = "Command2"
           ADD OBJECT command3 AS COMMANDBUTTON WITH ;
              TOP = 4, ;
              LEFT = 198, ;
              HEIGHT = 22, ;
              WIDTH = 96, ;
              CAPTION = "Work Around \&lt;2", ;
              DEFAULT = .F., ;
              NAME = "Command3"
           ADD OBJECT command4 AS COMMANDBUTTON WITH ;
              TOP = 4, ;
              LEFT = 294, ;
              HEIGHT = 22, ;
              WIDTH = 96, ;
              CAPTION = "Work Around \&lt;3", ;
              DEFAULT = .F., ;
              NAME = "Command4"
           ADD OBJECT command5 AS COMMANDBUTTON WITH ;
              LEFT=390, ;
              HEIGHT=22, ;
              CAPTION="E\&lt;xit", ;
              TOP=4, ;
              WIDTH=96, ;
              NAME = "Command5"
           PROCEDURE ACTIVATE
              THIS.DOCK(0)
           ENDPROC
           PROCEDURE command1.CLICK
              DO prnform IN PRNDEMO WITH "PREVIEW"
           ENDPROC
           PROCEDURE command2.CLICK
              DO prnform IN PRNDEMO WITH ""
           ENDPROC
           PROCEDURE command3.CLICK
              printfrm=CREATEOBJECT('prtform')
              printfrm.SHOW
           ENDPROC
           PROCEDURE command4.CLICK
              DO prnform IN PRNDEMO WITH "WORKAROUND2"
           ENDPROC
           PROCEDURE command5.CLICK
              CLEAR EVENTS
           ENDPROC
        ENDDEFINE

4. Create a program file named SETWORKAREA, using the following code:

        IF USED('billing')
           SELECT billing
        ENDIF
        retval=ALIAS()
        RETURN retval

5. In the Command window type:

        DO PRNDEMO

6. Clicking either the form or the "Failing Print" captioned toolbar button
  prints a report with incorrect data in the detail band. The number of records
  printed will be equal to the number of records contained in the orders table.

7. Clicking the command buttons captioned "Preview", "Work Around 1", "Work
  Around 2", or "Work Around 3" prints a report containing the correct data.

Additional query words: FxprintGeneral kbDSupport kbdse kbcode

======================================================================
Keywords          :  
Technology        : kbHWMAC kbOSMAC kbVFPsearch kbAudDeveloper kbVFP300bMac kbVFP300 kbVFP300b kbVFP500 kbVFP500a
Version           : MACINTOSH:3.0b; WINDOWS:3.0,3.0b,5.0,5.0a
Issue type        : kbprb
Solution Type     : kbnofix

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>