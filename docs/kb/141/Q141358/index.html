<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q141358: HOWTO: Use the Win32 DrawEdge API Function to Draw 3D Effects | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q141358: HOWTO: Use the Win32 DrawEdge API Function to Draw 3D Effects" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/141/Q141358/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/141/Q141358/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q141358: HOWTO: Use the Win32 DrawEdge API Function to Draw 3D Effects","url":"http://jeffpar.github.io/kbarchive/kb/141/Q141358/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q141358-howto-use-the-win32-drawedge-api-function-to-draw-3d-effects">Q141358: HOWTO: Use the Win32 DrawEdge API Function to Draw 3D Effects</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q141358
Product(s): Microsoft Visual Basic for Windows
Version(s): WINDOWS:4.0
Operating System(s): 
Keyword(s): kbAPI kbVBp kbVBp400 kbGrpDSVB kbDSupport
Last Modified: 11-JAN-2001

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Standard Edition, 32-bit, for Windows, version 4.0 
- Microsoft Visual Basic Professional Edition, 32-bit, for Windows, version 4.0 
- Microsoft Visual Basic Enterprise Edition, 32-bit, for Windows, version 4.0 
-------------------------------------------------------------------------------

SUMMARY
=======

The Win32 API features a new function called DrawEdge. Its purpose is to draw 3D
effects for all Windows applications. Microsoft Windows NT, Microsoft Windows
2000, Microsoft Windows 95, and Windows 98 all use the DrawEdge function for all
of their 3D effects. "The Windows Interface Guidelines for Software Design" (by
Microsoft Press) also suggests that all Windows programmers use the DrawEdge
function when adding 3D effects to their applications. The advantages of using
the DrawEdge function are:

- It provides a consistent 3D look and feel for all applications.

- It saves the programmer from having to convert their 3D code to a flat
  version for use with monochrome displays and printers.

- It insures that your 3D effects redraw properly when the system colors
  change.

- It allows you to avoid using and distributing THREED32.OCX to add 3D effects
  to your application.

- It allows for maximum portability to future versions of Windows.

MORE INFORMATION
================

The following code sample is a module of all of the declarations needed to use
the DrawEdge function with Visual Basic 4.0 for Windows.

  '***********************************************************************
  ' DrawEdge.bas - Contains API declarations and constants for the
  '                DrawEdge API function.
  '***********************************************************************
  Option Explicit
  Public Type RECT
          Left As Long
          Top As Long
          Right As Long
          Bottom As Long
  End Type

  Public Const BDR_RAISEDOUTER = &amp;H1
  Public Const BDR_SUNKENOUTER = &amp;H2
  Public Const BDR_RAISEDINNER = &amp;H4
  Public Const BDR_SUNKENINNER = &amp;H8

  Public Const BDR_OUTER = &amp;H3
  Public Const BDR_INNER = &amp;HC
  Public Const BDR_RAISED = &amp;H5
  Public Const BDR_SUNKEN = &amp;HA

  Public Const EDGE_RAISED = (BDR_RAISEDOUTER Or BDR_RAISEDINNER)
  Public Const EDGE_SUNKEN = (BDR_SUNKENOUTER Or BDR_SUNKENINNER)
  Public Const EDGE_ETCHED = (BDR_SUNKENOUTER Or BDR_RAISEDINNER)
  Public Const EDGE_BUMP = (BDR_RAISEDOUTER Or BDR_SUNKENINNER)

  Public Const BF_LEFT = &amp;H1
  Public Const BF_TOP = &amp;H2
  Public Const BF_RIGHT = &amp;H4
  Public Const BF_BOTTOM = &amp;H8

  Public Const BF_TOPLEFT = (BF_TOP Or BF_LEFT)
  Public Const BF_TOPRIGHT = (BF_TOP Or BF_RIGHT)
  Public Const BF_BOTTOMLEFT = (BF_BOTTOM Or BF_LEFT)
  Public Const BF_BOTTOMRIGHT = (BF_BOTTOM Or BF_RIGHT)
  Public Const BF_RECT = (BF_LEFT Or BF_TOP Or BF_RIGHT Or BF_BOTTOM)

  Public Const BF_DIAGONAL = &amp;H10

  ' For diagonal lines, the BF_RECT flags specify the end point of the
  ' vector bounded by the rectangle parameter.
  Public Const BF_DIAGONAL_ENDTOPRIGHT = (BF_DIAGONAL Or BF_TOP _
  Or BF_RIGHT)
  Public Const BF_DIAGONAL_ENDTOPLEFT = (BF_DIAGONAL Or BF_TOP Or BF_LEFT)
  Public Const BF_DIAGONAL_ENDBOTTOMLEFT = (BF_DIAGONAL Or BF_BOTTOM _
  Or BF_LEFT)
  Public Const BF_DIAGONAL_ENDBOTTOMRIGHT = (BF_DIAGONAL Or BF_BOTTOM _
  Or BF_RIGHT)

  Public Const BF_MIDDLE = &amp;H800        ' Fill in the middle
  Public Const BF_SOFT = &amp;H1000         ' For softer buttons
  Public Const BF_ADJUST = &amp;H2000       ' Calculate the space left over
  Public Const BF_FLAT = &amp;H4000         ' For flat rather than 3D borders
  Public Const BF_MONO = &amp;H8000         ' For monochrome borders

  Public Declare Function DrawEdge Lib "user32" (ByVal hdc As Long, _
      qrc As RECT, ByVal edge As Long, ByVal grfFlags As Long) As Boolean

  '***********************************************************************
  ' DrawEdge.frm - Demonstrates a simple example of how to use DrawEdge.
  '***********************************************************************
  Option Explicit

  '***********************************************************************
  ' Prepares the form and Picture1 for use with DrawEdge.
  '***********************************************************************
  Private Sub Form_Load()
      '-------------------------------------------------------------------
      ' Always set the ScaleMode to pixels when using API drawing
      ' functions.
      '-------------------------------------------------------------------
      ScaleMode = vbPixels
      With Picture1
          '---------------------------------------------------------------
          ' The next line is not required if you put your drawing code
          ' in the Paint event.
          '---------------------------------------------------------------
          .AutoRedraw = True
          '---------------------------------------------------------------
          ' Set the Backcolor, set the Borderstyle to none, and size
          ' the picture box to a more realistic button size.
          '---------------------------------------------------------------
          .BackColor = vb3DFace
          .BorderStyle = 0
          .Move 60, 10, 90, 30
          '-------------------- -------------------------------------------
          ' Make sure the picture box uses the pixel ScaleMode, and
          ' set the tag of the control to a caption for later use with
          ' DrawControl.
          '---------------------------------------------------------------
          .ScaleMode = vbPixels
          .Tag = "DrawEdge Test"
      End With
      '-------------------------------------------------------------------
      ' Draw the initial button.
      '-------------------------------------------------------------------
      DrawControl Picture1, Picture1.Tag, EDGE_RAISED
  End Sub

  '***********************************************************************
  ' When the picture box gets a click event, an etched box is drawn on
  ' the upper left corner of the form.
  '***********************************************************************
  Private Sub Picture1_Click()
      Dim r As RECT   ' used by DrawEdge to determine where to draw
      '-------------------------------------------------------------------
      ' Location of the etched box.
      '-------------------------------------------------------------------
      With r
          .Left = 10
          .Top = 10
          .Right = 50
          .Bottom = 50
      End With
      '-------------------------------------------------------------------
      ' Draw it.
      '-------------------------------------------------------------------
      DrawEdge hdc, r, EDGE_ETCHED, BF_RECT
  End Sub

  '***********************************************************************
  ' When the user presses the mouse down on the picture box a sunken edge
  ' is drawn to simulate a depresessed button.
  '***********************************************************************
  Private Sub Picture1_MouseDown(Button%, Shift%, X!, Y!)
      DrawControl Picture1, Picture1.Tag, EDGE_SUNKEN
  End Sub

  '***********************************************************************
  ' When the user releases the mouse over the picture box a standard
  ' button is drawn.
  '***********************************************************************
  Private Sub Picture1_MouseUp(Button%, Shift%, X!, Y!)
      DrawControl Picture1, Picture1.Tag, EDGE_RAISED
  End Sub

  '***********************************************************************
  ' The DrawControl helper function is designed to make it easier to
  ' draw a button on a picture box.
  '***********************************************************************
  Private Sub DrawControl(picControl As PictureBox, _
      strCaption As String, Optional vntEdge)

      Dim r As RECT    ' Holds the location of the DrawEdge rectangle.
      Dim intOffset%   ' Used to shift the caption when the button is
                       ' pressed.
      '-------------------------------------------------------------------
      ' If the user doesn't provide a Edge flag, then use a default value.
      '-------------------------------------------------------------------
      vntEdge = IIf(IsMissing(vntEdge), EDGE_RAISED, vntEdge)
      '-------------------------------------------------------------------
      ' Clear the picture control and determine where to draw the new
      ' rectangle and caption.
      '-------------------------------------------------------------------
      With picControl
          .Cls
          r.Left = .ScaleLeft
          r.Top = .ScaleTop
          r.Right = .ScaleWidth
          r.Bottom = .ScaleHeight
          If vntEdge = EDGE_SUNKEN Then intOffset = 2
          .CurrentX = (.ScaleWidth - .TextWidth(strCaption) _
                      + intOffset) / 2
          .CurrentY = (.ScaleHeight - .TextHeight(strCaption) _
                      + intOffset) / 2
      End With
      '-------------------------------------------------------------------
      ' Draw the caption, then draw the rectangle.
      '-------------------------------------------------------------------
      Picture1.Print strCaption
      DrawEdge picControl.hdc, r, CLng(vntEdge), BF_RECT
      '-------------------------------------------------------------------
      ' If AutoRedraw is True, then any drawing done by an API call cannot
      ' be seen until until the picture box gets refreshed.
      '-------------------------------------------------------------------
      If picControl.AutoRedraw Then picControl.Refresh
  End Sub

Additional query words: DRAW EDGE THREED SSPANEL

======================================================================
Keywords          : kbAPI kbVBp kbVBp400 kbGrpDSVB kbDSupport 
Technology        : kbVBSearch kbAudDeveloper kbVB400Search kbVB400
Version           : WINDOWS:4.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>