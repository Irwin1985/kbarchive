<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q156101: PRB: Caption Dithered If DisabledForeColor Equals ForeColor | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q156101: PRB: Caption Dithered If DisabledForeColor Equals ForeColor" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/156/Q156101/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/156/Q156101/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q156101: PRB: Caption Dithered If DisabledForeColor Equals ForeColor","url":"http://jeffpar.github.io/kbarchive/kb/156/Q156101/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q156101-prb-caption-dithered-if-disabledforecolor-equals-forecolor">Q156101: PRB: Caption Dithered If DisabledForeColor Equals ForeColor</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q156101
Product(s): Microsoft FoxPro
Version(s): WINDOWS:3.0,3.0b,5.0,6.0
Operating System(s): 
Keyword(s): kbvfp300 kbvfp500 kbvfp600
Last Modified: 14-DEC-1999

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 6.0 
-------------------------------------------------------------------------------

SYMPTOMS
========

For certain Visual FoxPro controls, if the DisabledForeColor property is set the
same as the ForeColor property when the control is disabled, the disabled
control's foreground color appears dithered. This behavior applies to command
buttons, labels, option buttons, and check boxes. This behavior also affects how
their captions appear.

WORKAROUND
==========

Set the DisabledForeColor property to be 1 (one) different from the ForeColor
property. For example, place the following code in the Init method for a check
box:

     this.DisabledForeColor = this.ForeColor + 1

STATUS
======

This behavior is by design.

MORE INFORMATION
================

This behavior occurs when the two properties are set the same. Because the
control is disabled, its caption appears dithered to indicate the control is
disabled. In certain cases, you may want the disabled control to appear nearly
identical to the enabled control. If so, use the technique in the WORKAROUND
section above.

Steps to Reproduce Behavior
---------------------------

1. Run the following code from a program (.prg) file:

        * Start of code example
        *
        PUBLIC oform1
        oform1=CREATEOBJECT("form1")
        oform1.SHOW

        DEFINE CLASS form1 AS FORM

          HEIGHT = 184
          WIDTH = 251
          AUTOCENTER = .T.
          CAPTION = "DisabledForeColor Test"
          NAME = "Form1"

          ADD OBJECT label1 AS LABEL WITH ;
            CAPTION = "Label1", ;
            LEFT = 24, ;
            TOP = 12, ;
            AUTOSIZE = .T., ;
            NAME = "Label1"

          ADD OBJECT optiongroup1 AS OPTIONGROUP WITH ;
            BUTTONCOUNT = 2, ;
            VALUE = 1, ;
            LEFT = 24, ;
            TOP = 48, ;
            AUTOSIZE = .T., ;
            NAME = "Optiongroup1", ;
            option1.CAPTION = "Option1", ;
            option1.VALUE = 1, ;
            option1.LEFT = 5, ;
            option1.TOP = 5, ;
            option1.AUTOSIZE = .T., ;
            option2.CAPTION = "Option2", ;
            option2.VALUE = 0, ;
            option2.LEFT = 5, ;
            option2.TOP = 24, ;
            option2.AUTOSIZE = .T.

          ADD OBJECT check1 AS CHECKBOX WITH ;
            TOP = 12, ;
            LEFT = 132, ;
            AUTOSIZE = .T., ;
            CAPTION = "Check1", ;
            NAME = "Check1"

          ADD OBJECT check2 AS CHECKBOX WITH ;
            TOP = 132, ;
            LEFT = 24, ;
            AUTOSIZE = .T., ;
            CAPTION = "Disable all", ;
            NAME = "Check2"

          ADD OBJECT check3 AS CHECKBOX WITH ;
            TOP = 132, ;
            LEFT = 132, ;
            AUTOSIZE = .T., ;
            CAPTION = "Add the 1?", ;
            NAME = "Check3"

          ADD OBJECT command1 AS COMMANDBUTTON WITH ;
            TOP = 60, ;
            LEFT = 120, ;
            HEIGHT = 27, ;
            WIDTH = 90, ;
            CAPTION = "Command1", ;
            NAME = "Command1"

          PROCEDURE optiongroup1.option1.REFRESH
            THIS.DISABLEDFORECOLOR=THIS.FORECOLOR + THISFORM.check3.VALUE
          ENDPROC

          PROCEDURE optiongroup1.option2.REFRESH
            THIS.DISABLEDFORECOLOR=THIS.FORECOLOR + THISFORM.check3.VALUE
          ENDPROC

          PROCEDURE check1.REFRESH
            THIS.DISABLEDFORECOLOR=THIS.FORECOLOR + THISFORM.check3.VALUE
          ENDPROC

          PROCEDURE check2.CLICK
            THISFORM.label1.DISABLEDFORECOLOR = ;
              THISFORM.label1.FORECOLOR + ;
              THISFORM.check3.VALUE  &amp;&amp;  Added because Label has no refresh
            THISFORM.REFRESH
            THISFORM.SETALL('enabled',!THISFORM.check1.ENABLED)
            THIS.ENABLED=.T.
          ENDPROC

          PROCEDURE command1.REFRESH
            THIS.DISABLEDFORECOLOR=THIS.FORECOLOR + THISFORM.check3.VALUE
          ENDPROC

        ENDDEFINE
        *
        * End of code example

2. Click the "Disable all" check box. The captions of the disabled controls
  appear dithered.

3. To demonstrate the workaround, clear the "Disable all" check box, click the
  "Add the 1?" check box, and click the "Disable all" check box. The control
  captions are not dithered but are still displayed with a 3-D effect.

NOTE: Depending on whether the "Add the 1?" check box is checked, the
DisabledForeColor property for each of the controls is set to either its
ForeColor (the "Add the 1?" check box is cleared) or its ForeColor + 1 (the "Add
the 1?" check box is cleared).

Additional query words: kbdse VFoxWin

======================================================================
Keywords          : kbvfp300 kbvfp500 kbvfp600 
Technology        : kbVFPsearch kbAudDeveloper kbVFP300 kbVFP300b kbVFP500 kbVFP600
Version           : WINDOWS:3.0,3.0b,5.0,6.0

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>