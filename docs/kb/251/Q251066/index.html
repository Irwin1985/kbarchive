<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=d2057ae472d963fbdcbf65805f43123bccb709dc">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.5.0 -->
<title>Q251066: HOWTO: Determine If a Computer Is a Microsoft Terminal Server | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.8.5" />
<meta property="og:title" content="Q251066: HOWTO: Determine If a Computer Is a Microsoft Terminal Server" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/251/Q251066/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/251/Q251066/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"headline":"Q251066: HOWTO: Determine If a Computer Is a Microsoft Terminal Server","url":"http://jeffpar.github.io/kbarchive/kb/251/Q251066/","description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="https://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q251066-howto-determine-if-a-computer-is-a-microsoft-terminal-server">Q251066: HOWTO: Determine If a Computer Is a Microsoft Terminal Server</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q251066
Product(s): Microsoft FoxPro
Version(s): WINDOWS:3.0,3.0b,5.0,5.0a,6.0
Operating System(s): 
Keyword(s): kbAPI kbvfp kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbGrpDSFox kbDSupport kbCode
Last Modified: 06-APR-2000

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 5.0a, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

This article shows how to determine if a computer is a Microsoft Windows NT or
Windows 2000 Terminal Server.

MORE INFORMATION
================

1. In Visual FoxPro, create a program file using the following code:

  *Start of Code
  LOCAL lTerminalServer

  lTerminalServer = IsTerminalServer()

  IF (lTerminalServer) THEN
     =MESSAGEBOX ("This Computer is a Terminal Server")
  ELSE
     =MESSAGEBOX("This Computer is not a Terminal Server")
  ENDIF

  FUNCTION IsTerminalServer
     * This function will determine if the machine is a Terminal Server.
     * It will return .T. if it is and .F. if not.

     * Constants that are needed for Registry functions
     #DEFINE HKEY_LOCAL_MACHINE -2147483646  &amp;&amp; BITSET(0,31)+2
     #DEFINE REG_DWORD 4	                   &amp;&amp; A 32-bit number.

     * WIN 32 API functions that are used
     DECLARE Integer RegOpenKey IN Win32API ;
        Integer nHKey, String @cSubKey, Integer @nResult
     DECLARE Integer RegQueryValueEx IN Win32API ;
        Integer nHKey, String lpszValueName, Integer dwReserved,;
        Integer @lpdwType, String @lpbData, Integer @lpcbData
     DECLARE Integer RegCloseKey IN Win32API ;
        Integer nHKey
     DECLARE GetVersionEx IN win32api STRING @OSVERSIONINFO

     * Local variables used
     LOCAL nErrCode	   &amp;&amp; Error Code returned from Registry functions
     LOCAL nKeyHandle	   &amp;&amp; Handle to Key that is opened in the Registry
     LOCAL lpdwValueType	   &amp;&amp; Type of Value that we are looking for
     LOCAL lpbValue	   &amp;&amp; The data stored in the value
     LOCAL lpcbValueSize	   &amp;&amp; Size of the variable
     LOCAL lpdwReserved	   &amp;&amp; Reserved Must be 0
     LOCAL cKey		   &amp;&amp; Key we need to open
     LOCAL cValueToGet	   &amp;&amp; Value to get
     LOCAL lTerminalServer   &amp;&amp; True if it is a Terminal Server
     LOCAL nTSEnabled	   &amp;&amp; Numeric value of TSEnabled
     LOCAL OSVersion	   &amp;&amp; OS Version
     LOCAL cMajorVersion	   &amp;&amp; Just the Version number i.e. 4 or 5
  	
     * Initialize the variables
     cKey = "System\CurrentControlSet\Control\Terminal Server"
     cValueToGet = "TSEnabled"
     nKeyHandle = 0				
     lpdwReserved = 0	       &amp;&amp; Must be 0
     lpdwValueType = REG_DWORD
     lpcbValueSize = 4	      &amp;&amp; DWORD is 4 bytes
     lTerminalServer = .F.      &amp;&amp; Assume it isn't a Terminal Server
     lpbValue = SPACE(4)

     nErrCode = RegOpenKey(HKEY_LOCAL_MACHINE, cKey, @nKeyHandle)
     * If the error code isn't 0, then the key doesn't exist or can't be opened.
     IF (nErrCode # 0) THEN
        RETURN .F.
     ENDIF

     * We need to check and see what version we are running NT 4 and Windows 2000 are different
     OSVersion = LongToStr(148) + REPLICATE(CHR(0), 144)
     =GetVersionEx(@OSVersion)
     nMajorVersion = StrToLong(SUBSTR(OSVersion, 5, 4))
     cMajorVersion = ALLTRIM(STR(nMajorVersion))

     * If it is NT 4 and we made it this far the machine is a Terminal Server
     IF (cMajorVersion = "4") THEN
        * Close the key when done.
        =RegCloseKey(nKeyHandle)
        RETURN .T.
     ENDIF

     * Get the value of TSEnabled. 1 is a Terminal  Server 0 isn't 
     nErrCode=RegQueryValueEx(nKeyHandle, cValueToGet, lpdwReserved, @lpdwValueType, @lpbValue, @lpcbValueSize)

     * Close the key when done.
     =RegCloseKey(nKeyHandle)

     IF (nErrCode # 0) THEN
        RETURN .F.
     ELSE
        nTSEnabled = StrToLong(lpbValue)
     ENDIF
  	
     * Check to see if it is a Terminal Server	
     IF (nTSEnabled = 1) THEN
         RETURN .T.
     ELSE
        RETURN .F.		
     ENDIF
  ENDFUNC	

  FUNCTION StrToLong
  * This function converts a String to a Long
     PARAMETERS cLongStr

     LOCAL nLoopVar, nRetval

     nRetval = 0
     FOR nLoopVar = 0 TO 24 STEP 8
        nRetval = nRetval + (ASC(cLongStr) * (2^nLoopVar))
        cLongStr = RIGHT(cLongStr, LEN(cLongStr) - 1)
     NEXT
  RETURN nRetval

  FUNCTION LongToStr
  * This function converts a long to a string
     PARAMETERS nLongVal

     LOCAL nLoopVar, strReturn

     strReturn = ""
     FOR nLoopVar = 24 TO 0 STEP -8
        strReturn = CHR(INT(nLongVal/(2^nLoopVar))) + strReturn
        nLongVal = MOD(nLongVal, (2^nLoopVar))
     NEXT
  RETURN strReturn
  *End of Code

2. Run the program created in step 1. The result appears in a message box as:

  This computer is a Terminal Server.

  -or-

  This computer is not a Terminal Server.

Additional query words:

======================================================================
Keywords          : kbAPI kbvfp kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbGrpDSFox kbDSupport kbCodeSnippet 
Technology        : kbVFPsearch kbAudDeveloper kbVFP300 kbVFP300b kbVFP500 kbVFP600 kbVFP500a
Version           : WINDOWS:3.0,3.0b,5.0,5.0a,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


<script type="text/javascript">
    (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
        (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
        m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
    })(window,document,'script','//www.google-analytics.com/analytics.js','ga');
    ga('create', 'UA-49658648-3', 'auto');
    ga('send', 'pageview');
</script>

</body>
</html>